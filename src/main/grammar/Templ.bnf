{
  parserClass="com.templ.templ.parsing.TemplParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Templ"
  psiImplClassSuffix="Impl"
  psiPackage="com.templ.templ.psi"
  psiImplPackage="com.templ.templ.psi.impl"

  elementTypeHolderClass="com.templ.templ.psi.TemplTypes"
  elementTypeClass="com.templ.templ.psi.TemplElementType"
  tokenTypeClass="com.templ.templ.psi.TemplTokenType"
  psiImplUtilClass="com.templ.templ.psi.TemplPsiImplUtil"

  tokens=[
      LBRACE='{'
      RBRACE='}'
      LPARENTH='('
      RPARENTH=')'
      HTML_DECL_START='templ'
      SCRIPT_DECL_START='script'
      CSS_DECL_START='css'
      INLINE_COMPONENT_START='@'
      COMMENT='regexp:(//.*|/\*(.|\n)*?\*/)'
  ]
}

root ::= go_root (go_root | html_decl)*
go_root ::= GO_ROOT_FRAGMENT
html_decl ::= HTML_DECL_START DECL_GO_TOKEN html_decl_body RBRACE
html_decl_body ::= (HTML_FRAGMENT | expr | if_cond | switch_stmt | for_loop | component)*

// https://templ.guide/syntax-and-usage/expressions and https://templ.guide/syntax-and-usage/template-composition
expr ::= BOOL_EXPR_START? LBRACE GO_EXPR RBRACE

// https://templ.guide/syntax-and-usage/if-else
if_cond ::= GO_IF_START_FRAGMENT html_decl_body else_if* else? RBRACE
else_if ::= GO_ELSE_IF_START_FRAGMENT html_decl_body
else ::= GO_ELSE_START_FRAGMENT html_decl_body

// https://templ.guide/syntax-and-usage/switch
switch_stmt ::= GO_SWITCH_START_FRAGMENT switch_case* switch_default? RBRACE
switch_case ::= GO_CASE_FRAGMENT html_decl_body
switch_default ::= GO_DEFAULT_FRAGMENT html_decl_body

// https://templ.guide/syntax-and-usage/loops
for_loop ::= GO_FOR_START_FRAGMENT html_decl_body RBRACE

// https://templ.guide/syntax-and-usage/template-composition
component ::= INLINE_COMPONENT_START GO_INLINE_COMPONENT component_children?
component_children ::= LBRACE html_decl_body RBRACE
